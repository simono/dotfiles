# Zsh config for interactive shells.
#
# Location: ~/.zshrc
#
# Simon Olofsson <simon@olofsson.de>
#

# History
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=$HISTSIZE

# Set some system dependent variables
case $(uname -s) in
	'Linux' )
		local lsOpts='--color=auto'
		local lsColors=$(dircolors -b)
		local rmOpts='-I'
		alias ack="ack-grep"
		alias so-mktemp='mktemp'
		;;
	*BSD )
		local lsOpts='-G'
		local lsColors=$LSCOLORS
		local rmOpts='-I'
		alias so-mktemp='mktemp -t sim'
		;;
	'Darwin' )
		local lsOpts='-G'
		local lsColors=$LSCOLORS
		alias so-mktemp='mktemp -t sim'
		;;
	* )
		echo "Unknown system: $(uname -s)"
		;;
esac

# Aliases
alias cp='nocorrect cp -iv'
alias df='df -h'
alias du='du -h'
alias ll='ls -l'
alias ls="ls -hF $lsOpts"
alias mkdir='nocorrect mkdir'
alias mv='nocorrect mv -iv'
alias rm="rm -v $rmOpts"
alias zmv='noglob zmv -vW'

# Custom aliases
alias so-du-dirs='du -s *(/)'
alias so-reset='cd; clear'
alias so-svn-hdiff='so-svn-diff -rBASE:HEAD'
alias so-svn-hlog='svn log -rHEAD:BASE'
alias so-tmp='cd $(so-mktemp -d)'

# A Calculator
autoload -U zcalc

# Rename multiple files
autoload -U zmv

# The shell's MIME system
autoload -U zsh-mime-setup && zsh-mime-setup

# When typing an URL, escape characters like *
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# Colors
autoload -U colors && colors

for color in blue cyan green magenta red; do
	eval $color='%{$fg[${color}]%}'
done
local resetColor="%{${reset_color}%}";

# VCS Info
autoload -Uz vcs_info
local vcs_format="$magenta%1.1s$red%u%c$resetColor"
zstyle ':vcs_info:*' enable bzr git hg svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-revision true
zstyle ':vcs_info:*' formats "[$vcs_format]"
zstyle ':vcs_info:*' actionformats "[$vcs_format(%a)]"
# hub slows this down
zstyle ':vcs_info:git:*:-all-' command /usr/bin/git

# Format for folders
local folder="%(4~.%1~.%~)"

# Prompt
PS1='$blue%m$resetColor:$cyan$folder$resetColor${vcs_info_msg_0_}%(?.$green.$red)%#$resetColor '

# Use vi bindings
bindkey -v

# Remove all bindings beginning with an escape character (probably cursor keys)
bindkey -rpM viins '^['
bindkey -rpM vicmd '^['

# vim-like undo and redo
bindkey -M vicmd 'u' undo
bindkey -M vicmd '^R' redo

# Some keys to change the case
bindkey -M vicmd 'gu' down-case-word
bindkey -M vicmd 'gU' up-case-word
bindkey -M vicmd 'g~' vi-oper-swap-case

# Start in normal (cmd) mode
zle-line-init() {
	zle -K vicmd
}
zle -N zle-line-init

# Show the vi mode on the right side and update it
zle-keymap-select() {
	RPS1="${${KEYMAP/(main|viins)/-- INSERT --}/vicmd/}"
	RPS2=$RPS1
	zle reset-prompt
	RPS1=""
	RPS2=$RPS1
}
zle -N zle-keymap-select

# Edit the command line
autoload -U edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

# Options, see zshoptions(1)
# Changing Directories
setopt auto_cd auto_pushd cdable_vars pushd_ignore_dups pushd_silent pushd_to_home
# Completion
setopt auto_menu complete_in_word
# Expansion and Globbing
setopt extended_glob nomatch
# History
setopt append_history hist_ignore_all_dups hist_reduce_blanks hist_verify
# Input/Output
setopt correct_all
unsetopt flow_control
# Job Control
setopt long_list_jobs
unsetopt notify
# Prompting
setopt prompt_subst transient_rprompt
# Zle
unsetopt beep

# Completion, see zshcompsys(1)
autoload -Uz compinit && compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' complete 1
zstyle ':completion:*' completer _list _oldlist _expand _complete _ignored _match _correct _approximate _prefix
zstyle ':completion:*' condition 0
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd .. directory
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ${(s.:.)lsColors}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' match-original both
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=** r:|=**' 'l:|=* r:|=*'
zstyle ':completion:*' max-errors 2 numeric
zstyle ':completion:*' menu select=4
zstyle ':completion:*' original false
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' substitute 1
zstyle ':completion:*' use-cache 1
zstyle ':completion:*' verbose true
zstyle ':completion:*:corrections' format '%B%d (errors: %e)%b'
# only complete named directories (instead of user directories)
zstyle ':completion:*:-tilde-:*' tag-order named-directories

# Setup screen
if [[ $TERM == screen* ]]; then

	# Update screen's window title
	update_window_title() {
		print -nP "\ek$1\e\\"
	}

	# Display the full command string
	preexec() {
		update_window_title "$1"
	}

	# Extract path when showing a prompt
	precmd() {
		vcs_info
		update_window_title "$folder%#"
		# Ring the bell before displaying the prompt
		echo -ne '\a'
	}
fi

# Functions

git() { hub "$@" }

so-diff() { diff -ruN "$@" | $PAGER }

so-svn-diff() {
	# Remove carriage returns from the diff
	svn diff "$@" | tr -d '\r' | $PAGER
}

so-xml-format() {
	for f in $@; do
		if [[ -f $f ]]; then
			xmllint --format "$f" --output "$f"
		else
			echo "$f is not a file."
		fi
	done
}

so-take() { mkdir -p "$1" && cd "$1" }

so-svn-ls() { svn st | sed -nE "s/^[$1][[:space:]]+//p" }

# Other stuff
[[ -f /etc/zsh_command_not_found ]] && source /etc/zsh_command_not_found
